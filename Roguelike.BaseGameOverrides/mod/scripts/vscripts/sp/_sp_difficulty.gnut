untyped
global function SPDifficulty_Init

global function UpdateNPCForSpDifficulty

global function SPMP_Callback_ForceAIMissPlayer
global function UpdateSpDifficulty
global function GetSpDifficulty
global function GetDamageScalarByDifficulty
global function SetProficiencyFromSPDifficulty
global function SelectDifficulty
global function GetLowestDifficultyThisMap
global function UpdateMercTitanHealthForDifficulty
global function Roguelike_QuickDeathInsteadOfKill
global function AddKillsAndMoney

const float FRIENDLY_TVT_DAMAGE_SCALE = 0.2 // scale damage dealt by militia npc titans against IMC
const float BT_TVT_DAMAGE_SCALE = 1.0 // scale damage dealt by BT npc against IMC titans
const float PLAYER_TITAN_PVT_OUT_OF_VIEW_DAMAGE_FORGIVENESS = 0.4 // damage is multiplied by this amount for out of view enemies dealing damage to player (as titan)
const float MFP_CHANCE_TO_HIT = 0.5 // Extra chance to miss if player is fast.

// ok chat you might be wondering "hmmm why the fuck did you give damage resist on all difficulties"
// WELL, i dont want people to be punished for being aggressive. high damage multipliers mean
// that the player dies before they can actually retaliate, forcing them to basically fuckin hide
// and cower. i want them to get CLOSE AND PERSONAL, not fuckin hide.
const float DIFFICULTY_EASY_DAMAGE_SCALAR = 0.75 // damage against player is multiplied by this number on Easy.
const float DIFFICULTY_NORMAL_DAMAGE_SCALAR = 1.0 // damage against player is multiplied by this number on Normal.
const float DIFFICULTY_HARD_DAMAGE_SCALAR = 1.0 // damage against player is multiplied by this number on Hard.
// NO INCREASE HERE, TITANS HAVE 0 CD HERE LOL
const float DIFFICULTY_MASTER_DAMAGE_SCALAR = 1.0 // damage against player is multiplied by this number on Master.
const float PLAYER_TITAN_DAMAGE_SCALAR = 1.0 // damage against player titan multiplied by this

struct
{
	int lowestDifficulty = 999
	float maxDamagePerHit
} file

void function UpdateSpDifficulty( entity player )
{
	int difficulty = GetSpDifficulty()

	// somewhere between hard and master i think
	NPCSetAimConeFocusParams( 1.0, 3.0 )
	NPCSetAimPatternFocusParams( 2, 0.5, 0.8 )
	NPCSetReacquireParams( 2.5, 2 ) // forget quickly about targets
	file.maxDamagePerHit = 50 // REALLY low damage per hit. players should

	if ( IsAlive( player ) && IsPilot( player ) )
		ScaleHealthForDifficulty( player, difficulty )

	array<entity> npcs = GetNPCArray()
	foreach ( npc in npcs )
	{
		UpdateNPCForSpDifficulty( npc )
	}

	if ( DevStartPoints() )
	{
		foreach ( player in GetPlayerArray() )
		{
			Remote_CallFunction_NonReplay( player, "ServerCallback_UpdateDifficulty" )
		}
	}
}


void function SPDifficulty_Init()
{
	Assert( !IsMultiplayer() )

	AddDamageCallback( "npc_titan", NPCTitan_OnDamaged )
	AddDamageCallback( "player", SpPlayer_OnDamaged )

	// titans are easier when player is doomed
	AddSpawnCallback( "npc_titan", NPCTitan_OnSpawned )
	AddCallback_OnTitanDoomed( OnTitanDoomed )
	AddCallback_OnNPCKilled( NPCKilled )
	AddClientCommandCallback( "ClientCommand_RequestSPDifficultyChange", ClientCommand_RequestSPDifficultyChange )

	file.lowestDifficulty = GetSpDifficulty()
}

void function NPCKilled( entity ent, entity attacker, var damageInfo )
{
	entity sourceWeapon = null
	if (attacker.IsPlayer())
		sourceWeapon = Roguelike_FindWeaponForDamageInfo( damageInfo )
	if (attacker.IsPlayer() && !attacker.IsTitan() && Roguelike_HasMod( attacker, "dash_on_kill" ))
	{
		float dodgePowerDrain = 100.0
		if (Roguelike_HasMod( attacker, "double_dash" ))
			dodgePowerDrain = 50.0

		attacker.Server_SetDodgePower( min( attacker.GetDodgePower() + dodgePowerDrain, 100.0 ) )
	}
	int cashReward = 0
	int killReward = 0
	int cashBonus = 0
	if ("attackedByPlayer" in ent.s)
	{
		entity player = GetFirstPlayer()
		if (ent.IsTitan())
		{
			cashReward = 50
			killReward = 25
		}
		else if (IsSuperSpectre( ent ))
		{
			cashReward = 25
			killReward = 10
		}
		else if (ent.GetClassName() == "npc_marvin" || AllowGruntKills(GetMapName()))
		{
			cashReward = 10
			killReward = 2
			if (ent.GetClassName() == "npc_marvin" && Roguelike_HasMod( player, "morality" ))
			{
				cashReward *= 3
			}
		}
		if (Roguelike_HasMod( attacker, "physical_spread" ) && !attacker.IsTitan() && Roguelike_GetDamageInfoElement( damageInfo ) == RoguelikeElement.physical
			&& IsValid(sourceWeapon))
		{
			cashBonus += 50
		}
		if (Roguelike_GetWeaponPerks( sourceWeapon ).contains("cash_bonus"))
		{
			cashBonus += 50
		}
	}

	if (!attacker.IsPlayer())
		return

	int multikill = 0
	if (IsValid(sourceWeapon))
	{
		if (!("multikill" in sourceWeapon.s) || sourceWeapon.s.multikillTime + 5 < Time())
		{
			sourceWeapon.s.multikill <- 1
			sourceWeapon.s.multikillTime <- Time()
			multikill = 1
		}
		else
		{
			sourceWeapon.s.multikill++
			sourceWeapon.s.multikillTime = Time()
			multikill = expect int(sourceWeapon.s.multikill)
		}
		if (Roguelike_GetWeaponPerks( sourceWeapon ).contains("rush"))
		{
			if (StatusEffect_Get( attacker, eStatusEffect.speed_boost ) <= 0.2)
				StatusEffect_AddTimed( attacker, eStatusEffect.speed_boost, 0.2, 8.0, 1.0 )
		}
		if (Roguelike_GetWeaponPerks( sourceWeapon ).contains("kill_self_dmg"))
		{
			RSE_Apply( attacker, RoguelikeEffect.kill_self_dmg, RSE_Get( attacker, RoguelikeEffect.kill_self_dmg ) + 1.0, 15.0, 0.0)
		}
		if (Roguelike_HasMod( attacker, "mag_size_inf" ) )
		{
			RSE_Apply( attacker, RoguelikeEffect.mag_size_inf, RSE_Get( attacker, RoguelikeEffect.mag_size_inf ) + 1.0 )
		}
		if (Roguelike_GetWeaponPerks( sourceWeapon ).contains("heal_clip"))
		{
			if (multikill > 0 && multikill % 3 == 0)
			{
				HealPlayer( attacker, 25 )
			}
		}
		if (Roguelike_GetWeaponPerks( sourceWeapon ).contains("load_other_weapon_on_kill"))
		{
			foreach (entity weapon in attacker.GetMainWeapons())
			{
				if (weapon == sourceWeapon)
					continue
				
				weapon.SetWeaponPrimaryClipCount(weapon.GetWeaponPrimaryClipCountMax())
				MessageToPlayer( attacker, eEventNotifications.WEAP_GotAmmo, weapon, 500 )
				break
			}
		}
		if (Roguelike_GetWeaponPerks( sourceWeapon ).contains("explode_kill"))
		{
			print("sedoosh")
			EmitSoundAtPosition( TEAM_UNASSIGNED, ent.GetWorldSpaceCenter(), "Explo_FragGrenade_Impact_3P" )
			PlayFX( $"P_impact_exp_FRAG_air", ent.GetWorldSpaceCenter(), <0,0,0> )

			RadiusDamage(
				ent.GetWorldSpaceCenter(),									// center
				attacker,					// attacker
				DamageInfo_GetInflictor( damageInfo ),								// inflictor
				40,		// damage
				500,		// damageHeavyArmor
				5,		// innerRadius
				320,		// outerRadius
				0,										// flags
				0,										// distanceFromAttacker
				20000,										// explosionForce
				DF_EXPLOSION,							// scriptDamageFlags
				eDamageSourceId.damagedef_frag_drone_explode )			// scriptDamageSourceIdentifier
		}
	}

	bool flameCoreDamage = DamageInfo_GetDamageSourceIdentifier( damageInfo ) == eDamageSourceId.mp_titancore_flame_wave
	if (IsValid(DamageInfo_GetInflictor( damageInfo )))
	{
		flameCoreDamage = flameCoreDamage || ("eruption" in DamageInfo_GetInflictor(damageInfo).s)
	}
	if (RSE_Get( ent, RoguelikeEffect.core_on_kill ) > 0 && attacker.IsTitan() && ent.IsTitan())
	{
		printt("AGAIN!")
		// reset core
		SoulTitanCore_SetNextAvailableTime( attacker.GetTitanSoul(), RSE_Get( ent, RoguelikeEffect.core_on_kill ) )
	}


	if (attacker.IsPlayer() && attacker.IsTitan() && ent.IsTitan() && Roguelike_HasDatacorePerk( attacker, "second_wind" ))
	{
		if (attacker.GetTitanSoul().IsDoomed())
			UndoomTitan( attacker, 1 )
		MessageToPlayer( attacker, eEventNotifications.Rodeo_TitanPickedUpBattery, null, false )
	}

	if (Roguelike_HasMod( attacker, "physical_gain" ) && !attacker.IsTitan() && Roguelike_GetDamageInfoElement( damageInfo ) == RoguelikeElement.physical
		&& IsValid(sourceWeapon))
	{
		RSE_Apply( attacker, RoguelikeEffect.physical_spread, 1.0, 20.0, 0.0 )
		RSE_Apply( attacker, RoguelikeEffect.physical_dmg, 1.0, 10.0, 0.0 )
	}
	if (Roguelike_HasMod( attacker, "energy_gain" ) && !attacker.IsTitan() && Roguelike_GetDamageInfoElement( damageInfo ) == RoguelikeElement.electric
		&& IsValid(sourceWeapon))
	{
		int ammoToAdd = maxint(sourceWeapon.GetWeaponPrimaryClipCountMax() / 5, 1)
		sourceWeapon.SetWeaponPrimaryClipCount( min(sourceWeapon.GetWeaponPrimaryClipCount() + ammoToAdd, sourceWeapon.GetWeaponPrimaryClipCountMax()) )
		
		RSE_Apply( attacker, RoguelikeEffect.electric_dmg, 1.0, 10.0, 0.0 )
	}
	if (Roguelike_HasMod( attacker, "fire_gain" ) && !attacker.IsTitan() && Roguelike_GetDamageInfoElement( damageInfo ) == RoguelikeElement.fire
		&& IsValid(sourceWeapon))
	{
		HealPlayer( attacker, 15 )
		
		RSE_Apply( attacker, RoguelikeEffect.fire_dmg, 1.0, 10.0, 0.0 )
	}
	if (Roguelike_HasMod( attacker, "fire_spread" ) && !attacker.IsTitan() && Roguelike_GetDamageInfoElement( damageInfo ) == RoguelikeElement.fire
		&& IsValid(sourceWeapon))
	{
		vector origin = ent.GetWorldSpaceCenter()
		thread void function() : (attacker, sourceWeapon, origin)
		{
			for (int i = 0; i < 10; i++)
			{
				RadiusDamage(
					origin,									// center
					attacker,					// attacker
					attacker,								// inflictor
					20,		// damage
					20,		// damageHeavyArmor
					320,		// innerRadius
					320,		// outerRadius
					0,										// flags
					0,										// distanceFromAttacker
					0,										// explosionForce
					DF_EXPLOSION | DF_NO_SELF_DAMAGE,							// scriptDamageFlags
					eDamageSourceId.molotov )			// scriptDamageSourceIdentifier
				wait 0.5
			}
		}()
	}
	if (Roguelike_HasMod( attacker, "energy_spread" ) && !attacker.IsTitan() && Roguelike_GetDamageInfoElement( damageInfo ) == RoguelikeElement.electric
		&& IsValid(sourceWeapon))
	{
		vector origin = ent.GetWorldSpaceCenter()
		RadiusDamage(
			origin,									// center
			attacker,					// attacker
			attacker,								// inflictor
			40,		// damage
			40,		// damageHeavyArmor
			5,		// innerRadius
			320,		// outerRadius
			0,										// flags
			0,										// distanceFromAttacker
			0,										// explosionForce
			DF_EXPLOSION,							// scriptDamageFlags
			eDamageSourceId.molotov )			// scriptDamageSourceIdentifier
	}
	if (Roguelike_HasMod( attacker, "grenade_cd" ) && !attacker.IsTitan())
	{
		entity ordnance = attacker.GetOffhandWeapon(0)
		if (IsValid(ordnance))
			RestoreCooldown( ordnance, 0.1111 )
	}
	if (Roguelike_HasMod( attacker, "tactical_cd" ) && !attacker.IsTitan())
	{
		entity tactical = attacker.GetOffhandWeapon(1)
		if (IsValid(tactical))
		{
		float regenRate = tactical.GetWeaponSettingFloat(eWeaponVar.regen_ammo_refill_rate)
		tactical.SetWeaponPrimaryClipCount( minint(tactical.GetWeaponPrimaryClipCount() + int(1.5 * regenRate), tactical.GetWeaponPrimaryClipCountMax()) )
		}
	}
	if (Roguelike_HasMod( attacker, "tactical_cd" ) && !attacker.IsTitan())
	{
		entity tactical = attacker.GetOffhandWeapon(1)
		if (IsValid(tactical))
		{
		float regenRate = tactical.GetWeaponSettingFloat(eWeaponVar.regen_ammo_refill_rate)
		tactical.SetWeaponPrimaryClipCount( minint(tactical.GetWeaponPrimaryClipCount() + int(1.5 * regenRate), tactical.GetWeaponPrimaryClipCountMax()) )
		}
	}

	switch (Roguelike_GetRunModifier("cash_gain"))
	{
		case 1:
			cashReward = (cashReward * 9) / 10
			break
		case 2:
			cashReward = (cashReward * 8) / 10
			break
		case 3:
			cashReward = (cashReward * 7) / 10
			break
	}
	AddKillsAndMoney( GetFirstPlayer(), killReward, cashReward * (100 + cashBonus) / 100 )
}

void function AddKillsAndMoney( entity attacker, int kills, int money )
{
	int curKills = expect int(GetServerVar( "roguelikeKills" ))

	SetServerVar( "roguelikeKills", curKills + kills )

	Remote_CallFunction_NonReplay( attacker, "ServerCallback_Roguelike_AddMoney", money )
}

void function NPCTitan_OnSpawned( entity titan )
{
	titan.SetNPCFlag( NPC_NO_WEAPON_DROP, true )

	foreach ( p in GetPlayerArray() )
	{
		entity playerTitan = GetTitanFromPlayer( p )
		if ( IsAlive( playerTitan ) && GetDoomedState( playerTitan ) )
		{
			titan.kv.WeaponProficiency = titan.ai.playerDoomedProficiency
		}
	}
}

void function OnTitanDoomed( entity titan, var damageInfo )
{
	if ( titan.IsPlayer() || titan.GetBossPlayer() != null )
	{
		LowerNPCTitanProficiency()
	}
}

void function NPCTitan_OnDamaged( entity titan, var damageInfo )
{
	if ( DamageInfo_GetForceKill( damageInfo ) )
		return

	if ( titan.GetTeam() == TEAM_IMC )
	{
		entity attacker = DamageInfo_GetAttacker( damageInfo )
		if ( attacker.IsNPC() && attacker.IsTitan() )
		{
			if ( IsAlive( attacker.GetBossPlayer() ) )
				DamageInfo_ScaleDamage( damageInfo, BT_TVT_DAMAGE_SCALE )
			else
				DamageInfo_ScaleDamage( damageInfo, FRIENDLY_TVT_DAMAGE_SCALE ) // friendly titan only does 20% damage
		}
	}
	else
	{
		if ( IsAlive( titan.GetBossPlayer() ) )
		{
			// its BT! Scale incoming damage so BT doesn't die right away
			float healthRatio = float( titan.GetHealth() ) / titan.GetMaxHealth()
			float scale = GraphCapped( healthRatio, 0.4, 0.7, 0.20, 1.00 )
			DamageInfo_ScaleDamage( damageInfo, scale )
		}
	}
}

void function SpPlayer_OnDamaged( entity player, var damageInfo )
{
	// reduce incoming damage if you recently loaded a save
	DamageInfo_ScaleDamage( damageInfo, LoadSaveTimeDamageMultiplier() )

	entity attacker = DamageInfo_GetAttacker( damageInfo )

	if (player.uncloakOnDamage)
	{
		DisableCloak( player, 0.5 )
		
		entity weapon = player.GetOffhandWeapon(OFFHAND_LEFT)
		if ( IsValid( weapon ) && weapon.GetWeaponClassName() == "mp_ability_cloak" )
		{
			player.TakeOffhandWeapon( OFFHAND_LEFT )
			player.GiveOffhandWeapon( "mp_ability_cloak", OFFHAND_LEFT )
			weapon = player.GetOffhandWeapon( OFFHAND_LEFT )
			weapon.SetWeaponPrimaryClipCountAbsolute( 0 )
		}
	}

	if ( attacker == player )
		return

	const array<string> badTriggers = ["trigger_hurt", "trigger_quickdeath", "worldspawn"]
	int damageSourceId = DamageInfo_GetDamageSourceIdentifier( damageInfo )

	if (attacker.GetClassName() == "trigger_hurt")
		printt(attacker.GetOrigin())

	if (Time() - GetLastDodgeTime() < 0.35 )
	{
		if (attacker.IsTitan())
			ServerToClientStringCommand( player, "mod_activated Dodge! 2 1 1 1" )
		DamageInfo_SetDamage( damageInfo, 0 )
		return
	}

	if ( Roguelike_HasMod( player, "speed_shield" ) && !player.IsTitan() && Length2D(player.GetVelocity()) > 546 )
	{
		ServerToClientStringCommand( player, "mod_activated Speed-Based Shield! 2 1 1 1" )
		DamageInfo_SetDamage( damageInfo, 0 )
		return
	}

	if (Roguelike_HasDatacorePerk( player, "polarity" ) && player.IsTitan())
	{
		if (RSE_Get( player, RoguelikeEffect.polarity_blue ))
			DamageInfo_ScaleDamage( damageInfo, 0.75 )
	}

	if (RSE_Get( attacker, RoguelikeEffect.kill_self_dmg ) > 0.0)
	{
		DamageInfo_ScaleDamage( damageInfo, 4.0 / (4.0 + RSE_Get( player, RoguelikeEffect.kill_self_dmg ) * 0.334) )
	}
	if ( Roguelike_HasMod( player, "rearm_reshield" ) && player.IsTitan() )
	{
		DamageInfo_ScaleDamage( damageInfo, 1.0 - RSE_Get( player, RoguelikeEffect.rearm_reshield ) )
	}

	if (Roguelike_HasMod( player, "love_diviiiiiiiiiiiiiiiiiiiiiiiiiiiides" ) && player.IsTitan())
	{
		if (IsTitanCoreFiring( player ) && player.GetOffhandWeapon(OFFHAND_EQUIPMENT).GetWeaponClassName() == "mp_titancore_shift_core")
		{
			DamageInfo_ScaleDamage( damageInfo, 0.4 )
		}
	} 

	if ((damageSourceId == eDamageSourceId.fall || badTriggers.contains(attacker.GetClassName())) && DamageInfo_GetForceKill( damageInfo ) && GetMapName() != "sp_s2s") // if you fall off in the ark, kinda your fault tbh =-=
	{
		printt("fell off")
		// hack. all deaths from triggers should be quickdeaths
		DamageInfo_SetForceKill( damageInfo, false )
		DamageInfo_SetDamage( damageInfo, 0 )
		thread Roguelike_QuickDeathInsteadOfKill( player )
		return
	}


	if (RSE_Get( player, RoguelikeEffect.offense_canister ) > 0.0)
		DamageInfo_ScaleDamage( damageInfo, 1.5 )
	
	DamageInfo_ScaleDamage( damageInfo, 1.0 + 0.25 * Roguelike_GetRunModifier("pain") )

	if ( player.IsTitan() )
	{
		int armor = Roguelike_GetStat( player, STAT_ARMOR )
		DamageInfo_ScaleDamage( damageInfo, Roguelike_GetTitanDamageMultiplier( armor ) )

		if (attacker.IsTitan() && IsMercTitan(attacker))
			DamageInfo_ScaleDamage( damageInfo, 1.25 )

		ScaleDamageToPlayerByDifficulty( damageInfo )

		DamageInfo_ScaleDamage( damageInfo, PLAYER_TITAN_DAMAGE_SCALAR )

		if ( IsHumanSized( attacker ) )
		{
			DamageInfo_ScaleDamage( damageInfo, PLAYER_TITAN_PVT_OUT_OF_VIEW_DAMAGE_FORGIVENESS )
		}
	}
	else
	{
		array<string> banned = [
			"npc_dropship",
			"npc_soldier",
			"npc_stalker",
			"npc_spectre"
		]
		// non-titans have damage reduced by 90%
		if (banned.contains(attacker.GetClassName()))
		{
			DamageInfo_ScaleDamage( damageInfo, 0.5 )
		}
		ScaleDamageToPlayerByDifficulty( damageInfo )

		float recentDamage = GetTotalDamageTakenInTime( player, 0.5 )
		float maxDamageAllowed = max( 5, file.maxDamagePerHit - recentDamage )

		if ( DamageInfo_GetDamage( damageInfo ) > maxDamageAllowed && !(Roguelike_HasMod( player, "titan_aptitude")) )
		{
			DamageInfo_SetDamage( damageInfo, maxDamageAllowed )
		}
	}
}

void function Roguelike_QuickDeathInsteadOfKill( entity player )
{
	vector origin = expect vector(player.s.lastGroundPosition)
	vector angles = expect vector(player.s.lastGroundAngles)
	const float FADE_TIME = 0.1
	Signal( player, "QuickDeath" )

	ScreenFade( player, 0, 0, 0, 255, FADE_TIME, 2.2, FFADE_IN | FFADE_PURGE )
	player.FreezeControlsOnServer()
	player.SetInvulnerable() // youre fucking kidding me.
	player.p.doingQuickDeath = true

	EmitSoundOnEntity( player, "quickdeath_fadeout_to_fadein" )
	EmitSoundOnEntity( player, "Player_Death_Begin")

	wait FADE_TIME

	player.DisableWeapon()
	player.SetOrigin( origin )
	player.SetAngles( angles )
	player.SetVelocity( <0,0,0> )

	Signal( player, "QuickDeathPlayerTeleported" )

	StopSoundOnEntity( player, "quickdeath_fadeout_to_fadein" )

	wait 2.1

	player.UnfreezeControlsOnServer()
	player.ClearInvulnerable()
	player.p.doingQuickDeath = false
	player.EnableWeapon()
	EmitSoundOnEntity( player, "Player_Death_PrespawnTransition" )
}

void function LowerNPCTitanProficiency()
{
	array<entity> npcs = GetNPCArrayOfTeam( TEAM_IMC )
	foreach ( ai in npcs )
	{
		if ( IsAlive( ai ) )
		{
			if ( !ai.IsTitan() )
				continue

			printt( ai + ": lowering proficiency to: " + ai.ai.playerDoomedProficiency )
			ai.kv.WeaponProficiency = ai.ai.playerDoomedProficiency
		}
	}
}

void function RestoreNPCTitanProficiency()
{
	array<entity> npcs = GetNPCArrayOfTeam( TEAM_IMC )
	foreach ( ai in npcs )
	{
		if ( IsAlive( ai ) )
		{
			if ( !ai.IsTitan() )
				continue

			ai.kv.WeaponProficiency = ai.ai.defaultProficiency
		}
	}
}

void function ScaleDamageToPlayerByDifficulty( var damageInfo )
{
	int difficulty = GetSpDifficulty()
	float scalar

	switch ( difficulty )
	{
		case DIFFICULTY_EASY:
			scalar = DIFFICULTY_EASY_DAMAGE_SCALAR
			break

		case DIFFICULTY_NORMAL:
			scalar = DIFFICULTY_NORMAL_DAMAGE_SCALAR
			break

		case DIFFICULTY_HARD:
			scalar = DIFFICULTY_HARD_DAMAGE_SCALAR
			break

		case DIFFICULTY_MASTER:
			scalar = DIFFICULTY_MASTER_DAMAGE_SCALAR
			break

		default:
			return
	}

	if (GetConVarBool("roguelike_stat_balance"))
		DamageInfo_ScaleDamage( damageInfo, 0.8 )

	DamageInfo_ScaleDamage( damageInfo, scalar )
}

float function GetDamageScalarByDifficulty()
{
	int difficulty = GetSpDifficulty()
	float scalar = DIFFICULTY_NORMAL_DAMAGE_SCALAR

	switch ( difficulty )
	{
		case DIFFICULTY_EASY:
			scalar = DIFFICULTY_EASY_DAMAGE_SCALAR
			break

		case DIFFICULTY_NORMAL:
			scalar = DIFFICULTY_NORMAL_DAMAGE_SCALAR
			break

		case DIFFICULTY_HARD:
			scalar = DIFFICULTY_HARD_DAMAGE_SCALAR
			break

		case DIFFICULTY_MASTER:
			scalar = DIFFICULTY_MASTER_DAMAGE_SCALAR
			break
	}

	return scalar
}

int function GetSpDifficulty()
{
	int difficulty = GetConVarInt( "sp_difficulty" )
	return int( clamp( difficulty, DIFFICULTY_EASY, DIFFICULTY_MASTER ) )
}

void function ScaleHealthForDifficulty( entity player, int difficulty )
{
	float scale = 1.0
	switch ( difficulty )
	{
		case 0:
			//scale = 1.5
			break
		case 1:
			//scale = 1.25
			break
		case 2:
		case 3:
			break
	}
	ScaleHealth( player, scale )
}

void function SetTitanProficiency( entity npcTitan )
{
	if ( npcTitan.GetAIClass() == AIC_TITAN_BUDDY )
	{
		npcTitan.kv.WeaponProficiency = eWeaponProficiency.PERFECT
	}
	else
	{
		// should be difficulty based numbers
		int difficulty = GetSpDifficulty()
		npcTitan.kv.accuracyMultiplier = 2 + GetConVarInt("sp_difficulty")

		if ( !IsBossTitan( npcTitan ) )
			npcTitan.kv.WeaponProficiency = eWeaponProficiency.PERFECT
		else
			npcTitan.kv.WeaponProficiency = eWeaponProficiency.PERFECT // tuned to not be exact perfect

		if ( npcTitan.HasKey( "ProficiencyAdjust" ) )
		{
			int proficiencyAdjust = int( npcTitan.kv.ProficiencyAdjust )
			int weaponProficiency = int( npcTitan.kv.WeaponProficiency )
			int newProf = int( clamp( weaponProficiency, eWeaponProficiency.POOR, eWeaponProficiency.PERFECT ) )

			// printt( npcTitan )
			// printt( npcTitan.kv.WeaponProficiency )
			// printt( newProf )

			npcTitan.kv.WeaponProficiency = newProf
		}
	}

	int weaponProficiency = int( npcTitan.kv.WeaponProficiency )

	npcTitan.ai.playerDoomedProficiency = int( clamp( weaponProficiency - 1, eWeaponProficiency.POOR, eWeaponProficiency.PERFECT ) )
	npcTitan.ai.defaultProficiency = weaponProficiency
}

void function UpdateNPCForSpDifficulty( entity npc )
{
	if ( npc.IsTitan() )
	{
		SetTitanProficiency( npc )
		UpdateMercTitanHealthForDifficulty( npc )

		return
	}

	SetProficiencyFromSPDifficulty( npc )
}

void function UpdateMercTitanHealthForDifficulty( entity npc )
{
	// do not scale ally health
	if (npc.GetTeam() == TEAM_MILITIA)
		return

	float healthMult = ("healthMult" in npc.s ? expect float(npc.s.healthMult) : 1.0)
	float multiplier = (IsMercTitan( npc ) && !GetDoomedState( npc )) ? 2.0 : 1.0

	float entPower = GetConVarFloat("power_enemy_def")
	multiplier *= (500 + entPower) / (500)

	multiplier *= healthMult
	if (VERBOSE_HEALTH_PRINTOUTS)
		printt("Health Multiplier:", multiplier)
	float flatBonus = GetConVarFloat("power_enemy_hp")
	switch (Roguelike_GetRunModifier("enemy_hp"))
	{
		case 2:
			flatBonus += 1500
		case 1:
			flatBonus += 1500
			break
	}
	switch (GetSpDifficulty())
	{
		case DIFFICULTY_EASY:
			RescaleHealthWithMultiplier( npc, 1.0 * multiplier, flatBonus )
			break
		case DIFFICULTY_NORMAL:
			RescaleHealthWithMultiplier( npc, 1.33334 * multiplier, flatBonus )
			break
		case DIFFICULTY_HARD:
			RescaleHealthWithMultiplier( npc, 1.66667 * multiplier, flatBonus )
			break
		case DIFFICULTY_MASTER:
			RescaleHealthWithMultiplier( npc, 1.66667 * multiplier, flatBonus )
			break
	}
}

bool function SPMP_Callback_ForceAIMissPlayer( entity npc, entity player )
{
	entity weapon = npc.GetActiveWeapon()
	if ( !IsValid( weapon ) )
		return false

/*
	float recentDamage = TotalDamageOverTime_BlendedOut( player, 1.5, 3.0 )

	float damageMissChance = GraphCapped( recentDamage, 20, 100, 0.0, 1.0 )
	if ( RandomFloat( 1.0 ) < damageMissChance )
		return true
*/

	float chanceToHit = 1.0

	if ( player.IsTitan() )
		return false

	vector velocity = player.GetVelocity()
	float speed = Length( velocity )

	if (!player.IsOnGround())
	{
		chanceToHit *= 0.75
	}

	vector entDif = npc.GetOrigin() - player.GetOrigin()
	float dist = Length( entDif )
	entDif = Normalize( entDif )
	velocity = Normalize( velocity )
	float dot = DotProduct( entDif, velocity )

	if ( speed > 260 )
		chanceToHit *= 0.5

	if ( speed > 350 )
		chanceToHit *= 0.25

	if ( dot < 0.8 && speed > 25 )
		chanceToHit *= 0.5

	float rand = RandomFloat( 1.0 )
	return rand > chanceToHit
}

void function SetProficiencyFromSPDifficulty( entity soldier )
{
	entity enemy = soldier.GetEnemy()
	int weaponProficiency = eWeaponProficiency.AVERAGE

	if ( IsAlive( enemy ) && enemy.IsPlayer() && !IsValidRocketTarget( enemy ) )
	{
		weaponProficiency = eWeaponProficiency.GOOD
	}


	soldier.kv.accuracyMultiplier = 1.5 + GetConVarInt("sp_difficulty") * 0.75

	if (!soldier.IsTitan())
	switch (GetSpDifficulty())
	{
		case DIFFICULTY_EASY:
			RescaleHealthWithMultiplier( soldier, 0.9 )
			break
		case DIFFICULTY_NORMAL:
			RescaleHealthWithMultiplier( soldier, 1.0 )
			break
		case DIFFICULTY_HARD:
			RescaleHealthWithMultiplier( soldier, 1.0 )
			break
		case DIFFICULTY_MASTER:
			RescaleHealthWithMultiplier( soldier, 1.0 )
			break
	}

	soldier.kv.WeaponProficiency = weaponProficiency
}

void function RescaleHealthWithMultiplier( entity npc, float multiplier, float flatBonus = 0.0 )
{
	if (!IsAlive( npc ))
		return
	float healthFrac = GetHealthFrac( npc )
	float health = 100.0
	if (!("baseHealth" in npc.s) && npc.IsTitan())
		return

	if (npc.IsTitan())
	{
		if (VERBOSE_HEALTH_PRINTOUTS && npc.IsTitan())
			printt("base health", float(npc.s.baseHealth), flatBonus, multiplier)
		health = float(npc.s.baseHealth)
	}
	else
	{
		health = float( npc.Dev_GetAISettingByKeyField( "Health" ) )
	}

	npc.SetMaxHealth( (health + flatBonus) * multiplier )
	npc.SetHealth( npc.GetMaxHealth() * healthFrac )
}

void function SelectDifficulty( int difficulty )
{
	array<entity> players = GetPlayerArray()
	if ( !players.len() )
		return

	printt( "Set difficulty to " + difficulty )
	SetConVarInt( "sp_difficulty", difficulty )

	file.lowestDifficulty = minint( file.lowestDifficulty, difficulty )

	foreach ( player in players )
	{
		UpdateSpDifficulty( player )
	}
}


bool function ClientCommand_RequestSPDifficultyChange( entity player, array<string> args )
{
	int difficulty
	if ( args.len() != 1 )
		return false
	difficulty = int( args[0] )
	SelectDifficulty( difficulty )
	return true
}


int function GetLowestDifficultyThisMap()
{
	return file.lowestDifficulty
}